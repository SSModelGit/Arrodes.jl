# construct a belief state out of the mdp parameters
beliefstate_for_pomdp(mdp::KAgentPOMDP) = KAgentBeliefUpdater(state_dims=length(mdp.start), env_dims=mdp.menv.M)
# since copy(::MCTSSolver) isn't defined for some reason, just use this instead of figuring out a copy extension
van_solver(params) = MCTSSolver(n_iterations=params[1], depth=20, exploration_constant=params[2])
dpw_solver(params) = DPWSolver(n_iterations=params[1], depth=20, exploration_constant=params[2])
function mcts_solver(pomdp::KAgentPOMDP; solver_params=[:van, 1000, 10.0])
    pomdp_bup = beliefstate_for_pomdp(pomdp)
    𝒮_mcts = @match solver_params[1] begin
        :van => BeliefMCTSSolver(van_solver(solver_params[2:end]), pomdp_bup)
        :dpw => BeliefMCTSSolver(dpw_solver(solver_params[2:end]), pomdp_bup)
        _    => BeliefMCTSSolver(van_solver(solver_params[2:end]), pomdp_bup)
    end
    return 𝒮_mcts
end

# define Deep Q solver (REINFORCE, DQN, and SoftQ)
function deep_q_solver(pomdp::KAgentPOMDP; solver_params=[:all, 10000, 2, 512])
    as = actions(pomdp)
    S = state_space(pomdp)
    A() = Flux.gpu(DiscreteNetwork(Chain(Dense(Crux.dim(S)..., 64, relu), Dense(64, 64, relu), Dense(64, length(as))), as; dev=Flux.gpu))
    V() = Flux.gpu(ContinuousNetwork(Chain(Dense(Crux.dim(S)..., 64, relu), Dense(64, 64, relu), Dense(64, 1))))

    solver_type, N, epochs, batch_size = solver_params[[1, 2, 3, 4]] # can also do solver_params[1:2] if I wanted - keeping this as later reminder for self on how to use indexing
    𝒮_net = @match solver_type begin
        :all => [REINFORCE(π=A(), S=S, N=N, ΔN=500, a_opt=(epochs=epochs,), interaction_storage=[]),
                 DQN(π=A(), S=S, N=N, interaction_storage=[]),
                 SoftQ(π=A(), α=Float32(0.1), S=S, N=N, ΔN=1, c_opt=(;epochs=epochs, batch_size=batch_size), interaction_storage=[])]
        :reinforce => [REINFORCE(π=A(), S=S, N=N, ΔN=500, a_opt=(epochs=epochs,), interaction_storage=[])]
        :dqn => [DQN(π=A(), S=S, N=N, interaction_storage=[])]
        :softq => [SoftQ(π=A(), α=Float32(0.1), S=S, N=N, ΔN=1, c_opt=(;epochs=epochs, batch_size=batch_size), interaction_storage=[])]
    end

    if !(solver_type==:all)
        return 𝒮_net[1]
    end
    return 𝒮_net
end

function _warn_on_buffer_mismatch(anon_data, mdp)
    # anon_data expected to be an ExperienceBuffer-like object with .data Dict
    if !hasproperty(anon_data, :data)
        @warn "IQL training: provided demo buffer has no `.data` field; cannot validate dtypes"
        return
    end
    D = anon_data.data
    # check presence
    for key in (:s, :sp, :r, :a)
        if !haskey(D, key)
            @warn "IQL training: buffer missing expected key" key=key
        end
    end

    # dtype checks
    if haskey(D, :s)
        if eltype(D[:s]) != Float32
            @warn "IQL training: state observations are not Float32; model may be Float32 leading to conversions" eltype=eltype(D[:s]) recommended=Float32
        end
        # dimension check against mdp state space
        try
            S = state_space(mdp)
            expected = Crux.dim(S)[1]
            actual = size(D[:s], 1)
            if expected != actual
                @warn "IQL training: state dimension mismatch between mdp and buffer" mdp_state_dim=expected buffer_state_dim=actual
            end
        catch err
            # ignore if state_space not available
        end
    end
    if haskey(D, :sp)
        if eltype(D[:sp]) != Float32
            @warn "IQL training: next-state observations are not Float32; model may be Float32 leading to conversions" eltype=eltype(D[:sp]) recommended=Float32
        end
    end
    if haskey(D, :r)
        if eltype(D[:r]) != Float32
            @warn "IQL training: rewards are not Float32; consider using Float32 rewards for Flux models" eltype=eltype(D[:r]) recommended=Float32
        end
    end
    if haskey(D, :a)
        if eltype(D[:a]) != Bool
            @warn "IQL training: action matrix is not Bool; expected Bool one-hot columns" eltype=eltype(D[:a]) recommended=Bool
        end
    end
end

function solver_from_type(pomdp::KAgentPOMDP, type::Symbol=:dpw; solver_params)
    @match type begin
        :mcts => mcts_solver(pomdp;   solver_params=solver_params)
        :dql  => deep_q_solver(pomdp; solver_params=solver_params)
        _     => mcts_solver(pomdp;   solver_params=[:dpw, 1000, 10.0])
    end
end

function deep_q_metrics(pomdp::KAgentPOMDP, 𝒮_net; solver_type::Symbol=:all)
    @time π_net = @match solver_type begin
        :all => map(x->solve(x, pomdp), 𝒮_net)
        _    => solve(𝒮_net, pomdp)
    end
    labels = @match solver_type begin
        :all => ["REINFORCE", "DQN", "SoftQ" ]
        :reinforce => ["REINFORCE"]
        :dqn => ["DQN"]
        :softq => ["SoftQ" ]
    end
    p = plot_learning(𝒮_net, title = "One-shot Agent π Training Curves", 
                        labels = labels)
    
    return π_net, p
end

function quick_policy_compute_for_pomdp(pomdp::KAgentPOMDP; solver_type::Symbol=:mcts, solver_params=[:dpw, 1000, 10.0])
    𝒮_pomdp = solver_from_type(pomdp, solver_type; solver_params=solver_params)
    π_pomdp = solve(𝒮_pomdp, pomdp)
    return 𝒮_pomdp, π_pomdp
end

function quick_IQL(kworld::KWorld, anon_data::ExperienceBuffer; plot_metrics::Bool=false)
    N = anon_data.elements
    mdp = get_agent(kworld, "ag1")

    _warn_on_buffer_mismatch(anon_data, mdp)

    as = actions(mdp)
    S = state_space(mdp)
    γ = Float32(discount(mdp))
    A() = DiscreteNetwork(Chain(Dense(S.dims[1], 64, relu), Dense(64, 64, relu), Dense(64, length(as))), as; dev=Flux.cpu)

    𝒟_iql = OnlineIQLearn(π=A(), 𝒟_demo=anon_data, S=S, γ=γ, N=anon_data.elements, ΔN=1, c_opt=(;epochs=1),reg=false,gp=false, log=(;period=50))

    π_iql = solve(𝒟_iql, mdp)

    if plot_metrics; f = plot_learning([𝒟_iql,], title="Results of IQ-Learning", labels=["iql",]); else; f = nothing; end
    return π_iql, 𝒟_iql, mdp, f
end

function quick_IQL(mdp::KAgentPOMDP, anon_data::ExperienceBuffer)
    N = anon_data.elements
    as = actions(mdp)
    S = state_space(mdp)
    γ = Float32(discount(mdp))

    _warn_on_buffer_mismatch(anon_data, mdp)

    A() = DiscreteNetwork(Chain(Dense(S.dims[1], 64, relu), Dense(64, 64, relu), Dense(64, length(as))), as; dev=Flux.cpu)

    𝒟_iql = OnlineIQLearn(π=A(), 𝒟_demo=anon_data, S=S, γ=γ, N=anon_data.elements, ΔN=1, c_opt=(;epochs=1),reg=false,gp=false, log=(;period=50))

    π_iql = solve(𝒟_iql, mdp)

    return π_iql, 𝒟_iql, mdp
end